plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    id 'application'
    id 'idea'
}

group 'ai.test'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.grpc:grpc-okhttp:1.15.0'
    compile 'io.grpc:grpc-protobuf-lite:1.15.0'
    compile 'io.grpc:grpc-stub:1.15.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.15.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task classifierClient(type: CreateStartScripts) {
    mainClassName = 'ai.test.classifier_client'
    applicationName = 'test-ai-classifier-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(classifierClient)
    fileMode = 0755
}